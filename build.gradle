plugins {
    id "java"
    id "groovy"
    id "application"
    id "idea"
    id "eclipse"
    id "jacoco"
    id "com.github.kt3k.coveralls" version "2.8.4"

    // FIXME: org.openjfx.javafxplugin currently doesn"t work with gradle 6.6+
    id "org.openjfx.javafxplugin" version "0.0.9"

    id "org.sonarqube" version "3.0"
}

group "thomashan.github.io"
version "0.0.1"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11


repositories {
    mavenCentral()
}

dependencies {
    implementation("org.codehaus.groovy:groovy-all:3.0.5") {
        exclude group: "junit", module: "junit"
    }

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"
}

test {
    useJUnitPlatform()
}

javafx {
    version = "15"
    modules = ["javafx.controls"]
}

application {
    mainModule = "io.github.thomashan.tradingchart"
    mainClass = "io.github.thomashan.tradingchart.CandleStickChartApp"
}

sonarqube {
    properties {
        property "sonar.projectKey", "thomashan_trading-charts"
        property "sonar.organization", "thomashan-github"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

sourceSets {
    bytewatcher {
    }
}

configurations {
    bytewatcherImplementation.extendsFrom testImplementation
    bytewatcherRuntimeOnly.extendsFrom testRuntimeOnly
//    bytewatcherRuntimeClasspath.extendsFrom testRuntimeClasspath
//    bytewatcherCompileClasspath.extendsFrom testCompileClasspath
//    bytewatcherCompileOnly.extendsFrom testCompileOnly
}

task bytewatcher(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.bytewatcher.output.classesDirs
    classpath = sourceSets.bytewatcher.runtimeClasspath
}

gradleEnterprise {
    buildScan {
        termsOfServiceUrl = "https://gradle.com/terms-of-service"
        termsOfServiceAgree = "yes"
    }
}
